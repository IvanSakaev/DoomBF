===== Это jump =====
[
Вся программа представляет собой N инструкций и M блоков кода.

Сначала программа выполняет первую инструкцию.
Потом программа циклично проходит по всем фрагментам и для каждого проверяет, должен ли он выполнится.
Потом выполняет вторую инструкцию и т.д.

1я ячейка всегда хранит 1. Для выхода из программы нужно задать ей 0.
2я ячейка содержит номер команды.
3я и 4я ячейки зарезервированы под if not.
Ячейки с 5ой по N+4 хранят 1 или 0 и указывают, должен ли выполниться конкретный фрагмент.

N = 7       - количество инструкций
M = 5       - количество различных блоков кода
]


+[>+
    Инструкции:
    Они выполняются по очереди
    Они должны только выбирать блок кода который выполнится в эту итерацию
    Количество стрелочек перед пробелом указывает на то какой блок по номеру надо запустить
    Стрелочки должны быть симметричны относительно плюса

    Инструкции могут иметь более сложную логику для выбора блока кода
    Инструкции начинают выполнение на 3ей ячейке и должны завершиться на ней же
            Инструкции начинаются здесь |   | А заканчиваются здесь
                                        V   V
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >>+<<           <<++++++++>]<   Плюсов после всех стрелочек должно быть ровно на один больше чем M
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >>>>+<<<<       <<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >>+<<           <<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >>>>>+<<<<<     <<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >+<             <<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >>>+<<<         <<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->       >>+<<           <<++++++++>]<

    ->[-]+>[-]+<<[>->-<]>[>-]<[-<   <->     >]<             После всех операций осуществляется выход из программы
                                                            В будущем лучше сделать выход отдельным блоком программы
                                                            А в случае если все инструкции выполнились начинать выполнять их с первой
    >>

    Блоки кода:
    В них можно использовать ячейки 3 и 4 а также ячейки выбора блоков
    Все ячейки выбора блоков должны быть в нуле после использования
    Однако не рекомендуется использовать зарезервированные ячейки
    поскольку в случае ошибки проблема может возникнуть в неизвестном месте
    Ячейка 5
    >[-
        ++++++++[>++++++++<-]>+.[-]<[-]
    ]
    Ячейка 6
    >[-
        ++++++++[>++++++++<-]>++.[-]<[-]
    ]
    Ячейка 7
    >[-
        ++++++++[>++++++++<-]>+++.[-]<[-]
    ]
    Ячейка 8
    >[-
        ++++++++[>++++++++<-]>++++.[-]<[-]
    ]
    Ячейка 9
    >[-
        ++++++++[>++++++++<-]>+++++.[-]<[-]
    ]
    <<<<<   Стрелок влево должно быть ровно M

    <<
<]
