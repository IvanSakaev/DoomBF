===== Это полноценная структура кода на brainfuck =====
[
Вся программа представляет собой N инструкций и M блоков кода.

Сначала программа выполняет первую инструкцию.
Потом программа циклично проходит по всем фрагментам и для каждого проверяет, должен ли он выполнится.
Потом выполняет вторую инструкцию и т.д.

1я ячейка всегда хранит 1. Для выхода из программы нужно задать ей 0.
2я ячейка содержит номер текущей инструкции.
3я и 4я ячейки зарезервированы под if not.
! if not использует 3ю и 4ю ячейку не только из 2ой, но и из 5ой
! не вставлять ничего между ними без доп. исправлений
5я ячейка указывает, какой фрагмент должен выполняться. 0 - никакой, 1 - первый и т.д

Следующие ячейки можно изменять из блоков (это повлияет на поведение программы):
6я ячейка - регистр A, с ней выполняются все операции и результат сохраняется в нее.
7я ячейка - регистр B, с ней выполняются все операции.
8я и 9я ячейки используются для операций с регистрами, после использования должны очищаться.
10я ячейка - номер блока, который нужно запустить следующим.
             Если ячейка хранит 0, выполняется следующая по счету инструкция.
             Если ячейка хранит номер блока, выполняется блок с этим номером,
             а в эту ячейку записывается значение 11ой ячейки. 11ая ячейка очищается.
             Не рекомендуется записывать в эту ячейку значение, если она не пустая.
11я ячейка - номер блока, который нужно выполнить после блока, указанного в 10ой ячейке.
             Может хранить не 0, только если 10я ячейка не 0.
             ПОКА НЕ ИСПОЛЬЗУЕТСЯ.
    Это нужно, чтобы реализовывать вызов функции из середины другой функции.
    Исходная функция разбивается на 2 блока, а функция, которую нужно вызвать, помещается в 3ий блок.
    1ый блок записывает в 10ую ячейку 3, а в 11ую ячейку 2.
    Таким образом, сначала вызовется нужная функция, а потом продолжит выполняться исходная.
    Однако так нельзя реализовать вызов функции из функции, которая была вызвана другой функцией.
    Рекурсия разрешена.


N = 7       - количество инструкций
M = 5       - количество различных блоков кода
]


+[>
    Обработка 10ой ячейки
    >>>>>>>>
    [
        #
        Отмена выполнения инструкций:
        <<<<<<<<+++++++>>>>>>>>        Плюсов должно быть ровно M
        Запись номер нужного блока в 5ую ячейку:
        [<<<<<+>>>>>-]
    ]
    <<<<<<<<+


    Инструкции:
    Они выполняются по очереди
    Они должны только выбирать блок кода который выполнится в эту итерацию
    Значение в 5ой ячейке указывает на то какой блок по номеру надо запустить
    Инструкции могут иметь более сложную логику для выбора блока кода
    Инструкции начинают выполнение на 5ой ячейке и должны завершиться на ней же
        Инструкции начинаются здесь |              | А заканчиваются здесь
                                    V              V
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>      +           <<<++++++++>]<   Плюсов после всех стрелочек должно быть ровно на один больше чем M
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>      ++          <<<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>      +++         <<<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>      ++++        <<<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>                  <<<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>                  <<<++++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->>                  <<<++++++++>]<

    ->[-]+>[-]+<<[>->-<]>[>-]<[-<       <->         >]<     После всех операций осуществляется выход из программы
                                                            В будущем лучше сделать выход отдельным блоком программы
                                                            А в случае если все инструкции выполнились начинать выполнять их с первой
    >>>         Ячейка 5


    Блоки кода:
    Выполнение каждого блока начинается на 5ой ячейке и должно заканчиваться там же
    Блоки являются функциями за исключением того что
     = из середины блока нельзя вызвать другой блок
     = блок не принимает аргументов
     = блок не может вернуть значение

    Блок 1      Инициализация чисел в 12 и 13 ячейке
    -<[-]+<[-]+>>[<-<->]<[<-]>[->
        >>>>>>>+++++++>++++<<<<<<<<
    <]>

    Блок 2      Перемещение чисел в регистры
    -<[-]+<[-]+>>[<-<->]<[<-]>[->
        >>>>>>>[<<<<<<+>>>>>>-]>[<<<<<<+>>>>>>-]<<<<<<<<
        >>>>>+++++<<<<<     Это указывает что после этого блока должен выполниться 5ый
                            а только потом начнут выполнение инструкции
    <]>

    Блок 3      Перемещение числа из регистра A в 13 ячейку
    -<[-]+<[-]+>>[<-<->]<[<-]>[->
        >[>>>>>>>+<<<<<<<-]<
    <]>

    Блок 4      Вывод
    -<[-]+<[-]+>>[<-<->]<[<-]>[->
        >>>>>>>[-]++++++++[>++++++++<-]>+.[-]<[-]<<<<<<<<
    <]>

    Блок 5      Добавление B к A
    -<[-]+<[-]+>>[<-<->]<[<-]>[->
        >>[>+>+<<-]>>[<<+>>-]<[<<+>>-]<<<
    <]>

    [-]<<<
<]
