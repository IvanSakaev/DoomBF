===== Это jump =====
[
Вся программа представляет собой N фрагментов.
Программа циклично проходит по всем фрагментам и проверяет, должен ли он выполнится.
1я ячейка всегда хранит 1.
2я ячейка содержит номер команды.
3я ячейка временно используется для различных операций.
4я ячейка содержит копию номера команды, она уменьшается на 1 с каждой командой.
5я и 6я ячейки зарезервированы под if not.
Ячейки с 7ой по N+6 хранят 1 или 0 и указывают, должен ли выполниться конкретный фрагмент.

N = 5
]


+[>+
    [>+>+<<-][>-<+]>>           Копирование из 2 в 4 ячейку


    Инструкции:
    Они выполняются по очереди
    Они должны только выбирать блок кода который выполнится в эту итерацию
    Количество стрелочек перед пробелом указывает на то какой блок по номеру надо запустить
    Стрелочки должны быть симметричны относительно плюса
    Плюсов после всех стрелочек должно быть больше чем N
    ->[-]+>[-]+<<[>->-<]>[>-]<[->   >>+<<       <<++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->   >>>>+<<<<   <<++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->   >>+<<       <<++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->   >>>>>+<<<<< <<++++++>]<
    ->[-]+>[-]+<<[>->-<]>[>-]<[->   >>>+<<<     <<++++++>]<

    ->[-]+>[-]+<<[>->-<]>[>-]<[-<   <<<->>>     >]<               После всех операций осуществляется выход из программы

    >>

    Блоки кода:
    В них можно использовать ячейки 3 и 4 а также ячейку блока
    Ячейку блока необходимо очистить после выполнения
    Ячейка 7
    >[
        >[-]<[-]++++++++[>++++++++<-]>+.[-]<[-]
    ]
    Ячейка 8
    >[
        >[-]<[-]++++++++[>++++++++<-]>++.[-]<[-]
    ]
    Ячейка 9
    >[
        >[-]<[-]++++++++[>++++++++<-]>+++.[-]<[-]
    ]
    Ячейка 10
    >[
        >[-]<[-]++++++++[>++++++++<-]>++++.[-]<[-]
    ]
    Ячейка 11
    >[
        >[-]<[-]++++++++[>++++++++<-]>+++++.[-]<[-]
    ]
    <<<<<<<
<<<]
